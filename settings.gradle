pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url = 'https://maven.neoforged.net/releases' }
        maven { url = "https://maven.quiltmc.org/repository/release" }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        // Minecraft Version
        def minecraftVersion = "1.21"

        // Mod Dependencies Versions
        // Common
        def jeiVersion = "19.0.0.7"
        def reiVersion = "16.0.729"
        def emiVersion = "1.1.7"
        def ae2Version = "18.1.3-alpha"
        def kjsVersion = "2100.7.0-build.30"
        def gtmVersion = "1.3.0"

        // NeoForge
        def neoForgeVersion = "21.0.16-beta"
        def registrateForgeVersion = "MC1.21-1.3.0+16"
        def jadeForgeVersion = "15.0.4+neoforge"
        def topForgeVersion = "1.20.4_neo-11.0.0-1"

        // Libs
        def parchmentVersion = "2024.06.02" // https://parchmentmc.org/docs/getting-started
        def shadowVersion = "7.1.2"
        def macheteVersion = "1.+"
        def configurationVersion = "2.2.3"
        def ldLibVersion = "1.0.25.r"
        def spotlessVersion = "7.0.0.BETA1"
        def neogradleVersion = "7.0.145"

        forge {
            version("forgeShortVersion", neoForgeVersion)
            def neoForge = version("neoForge", "${neoForgeVersion}")
            library("neoForge", "net.neoforged", "neoforge").versionRef(neoForge)

            def ldLib = version("ldlib", ldLibVersion)
            library("ldlib-neoforge", "com.lowdragmc.ldlib", "ldlib-neoforge-${minecraftVersion}").versionRef(ldLib)

            def registrate = version("registrate", registrateForgeVersion)
            library("registrate", "com.tterrag.registrate", "Registrate").versionRef(registrate)

            def jei = version("jei", jeiVersion)
            library("jei-common-api", "mezz.jei", "jei-${minecraftVersion}-common-api").versionRef(jei)
            library("jei-forge-api", "mezz.jei", "jei-${minecraftVersion}-neoforge-api").versionRef(jei)
            library("jei-forge-impl", "mezz.jei", "jei-${minecraftVersion}-neoforge").versionRef(jei)

            def rei = version("rei", reiVersion)
            library("rei-plugin", "me.shedaniel", "RoughlyEnoughItems-default-plugin-neoforge").versionRef(rei)
            library("rei-api", "me.shedaniel", "RoughlyEnoughItems-api-neoforge").versionRef(rei)
            library("rei-forge", "me.shedaniel", "RoughlyEnoughItems-neoforge").versionRef(rei)

            def emi = version("emi", emiVersion + "+" + minecraftVersion)
            library("emi", "dev.emi", "emi-neoforge").versionRef(emi)

            def jade = version("jade", jadeForgeVersion)
            library("jade","maven.modrinth", "jade").versionRef(jade)

            def theoneprobe = version("theoneprobe", topForgeVersion)
            library("theoneprobe", "mcjty.theoneprobe", "theoneprobe").versionRef(theoneprobe)

            def kubejs = version("kubejs", kjsVersion)
            library("kubejs", "dev.latvian.mods", "kubejs-neoforge").versionRef(kubejs)

            def configuration = version("configuration", configurationVersion)
            library("configuration", "dev.toma", "configuration-neoforge-${minecraftVersion}").versionRef(configuration)

            def ae2 = version("ae2", ae2Version)
            library("ae2", "appeng", "appliedenergistics2-neoforge").versionRef(ae2)

            def gtm = version("gtm", gtmVersion)
            library("gtm", "com.gregtechceu.gtceu", "gtceu-${minecraftVersion}").versionRef(gtm)
        }

        libs {
            version("parchment", parchmentVersion)
            version("minecraft", minecraftVersion)

            def neogradle = version("neogradle", neogradleVersion)
            plugin("neogradle", "net.neoforged.gradle.userdev").versionRef(neogradle)

            def shadow = version("shadow", shadowVersion)
            plugin("shadow", "com.github.johnrengelman.shadow").versionRef(shadow)

            def machete = version("machete", macheteVersion)
            plugin("machete", "io.github.p03w.machete").versionRef(machete)

            def spotless = version("spotless", spotlessVersion)
            plugin("spotless", "com.diffplug.spotless").versionRef(spotless)
        }
    }
}

rootProject.name = "${mod_id}"
