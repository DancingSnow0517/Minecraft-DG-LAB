pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url = "https://maven.neoforged.net/releases" }
        maven { url = "https://maven.quiltmc.org/repository/release" }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        // Minecraft Version
        def minecraftVersion = "1.21.1"

        // Mod Dependencies Versions
        // Common
        def jeiVersion = "19.17.0.193"
        def reiVersion = "16.0.744"
        def kjsVersion = "2101.7.1-build.181"

        // NeoForge
        def neoForgeVersion = "21.1.79"

        // Libs
        def parchmentVersion = "2024.11.17" // https://parchmentmc.org/docs/getting-started
        def shadowVersion = "8.1.1"
        def macheteVersion = "1.+"
        def configurationVersion = "5774720"
        def spotlessVersion = "7.0.0.BETA1"
        def modDevGradleVersion = "2.0.34-beta"

        forge {
            version("forgeShortVersion", neoForgeVersion)
            def neoForge = version("neoForge", "${neoForgeVersion}")
            library("neoForge", "net.neoforged", "neoforge").versionRef(neoForge)

            def jei = version("jei", jeiVersion)
            library("jei-common-api", "mezz.jei", "jei-${minecraftVersion}-common-api").versionRef(jei)
            library("jei-forge-api", "mezz.jei", "jei-${minecraftVersion}-neoforge-api").versionRef(jei)
            library("jei-forge-impl", "mezz.jei", "jei-${minecraftVersion}-neoforge").versionRef(jei)

            def rei = version("rei", reiVersion)
            library("rei-plugin", "me.shedaniel", "RoughlyEnoughItems-default-plugin-neoforge").versionRef(rei)
            library("rei-api", "me.shedaniel", "RoughlyEnoughItems-api-neoforge").versionRef(rei)
            library("rei-forge", "me.shedaniel", "RoughlyEnoughItems-neoforge").versionRef(rei)

            def kubejs = version("kubejs", kjsVersion)
            library("kubejs", "dev.latvian.mods", "kubejs-neoforge").versionRef(kubejs)

            def configuration = version("configuration", configurationVersion)
            library("configuration", "curse.maven", "configuration-444699").versionRef(configuration)
        }

        libs {
            version("parchment", parchmentVersion)
            version("minecraft", minecraftVersion)

            def modDevGradle = version("modDevGradle", modDevGradleVersion)
            plugin("modDevGradle", "net.neoforged.moddev").versionRef(modDevGradle)

            def shadow = version("shadow", shadowVersion)
            plugin("shadow", "com.github.johnrengelman.shadow").versionRef(shadow)

            def machete = version("machete", macheteVersion)
            plugin("machete", "io.github.p03w.machete").versionRef(machete)

            def spotless = version("spotless", spotlessVersion)
            plugin("spotless", "com.diffplug.spotless").versionRef(spotless)
        }
    }
}

rootProject.name = "${mod_id}"
